$ ->
  progress_bar = $('#time-left')

  if progress_bar.size() == 1
    seconds_left = progress_bar.data('seconds-left')
    timer = $.timer ->
      if $('#time-left').size() == 0
        timer.stop()
        return

      seconds_left -= 1

      time = new Date(seconds_left * 1000)
      min = if time.getMinutes() < 10 then '0'+time.getMinutes() else time.getMinutes()
      sec = if time.getSeconds() < 10 then '0'+time.getSeconds() else time.getSeconds()
      time_str = min + ':' + sec
      progress_bar.attr('aria-valuenow', time_str)
      progress_bar.text(time_str)

      progress = 100 * (600 - seconds_left) / 600
      progress_bar.css('width', progress+'%')

      if seconds_left == 0
        timer.stop()
        Turbolinks.visit(location.toString())

    timer.set
      time: 1000,
      autostart : true

    amounts = $('.bet-amount')
    amounts.on 'change mousemove keyup', (e) ->
      $(this).siblings('.preview-amount').text($(this).val())
      if e.which == 13
        $(this).next().children('button').click()
    amounts.find(':visible').trigger 'change'

    $('.new_bet').on('ajax:success', (e, data, status, xhr) ->
      if data.status == 'success'
        handle_bet data
      else
        notify 'warning', data.message
    ).on 'ajax:error', (e, xhr, status, error) ->
      notify 'danger', '<strong>An unexpected error occurred.</strong> ' + error

  if $('#locked').size() == 1
    refresh_timer = $.timer ->
      Turbolinks.visit(location.toString());
    refresh_timer.once 5000

  handle_bet = (data) ->
    form = $('#' + data.type + '_new_bet')
    method = form.find('input[name=_method]')
    choices = form.find('input[type=radio]')
    button = form.find('button[type=submit]')
    amount = form.find('input.bet-amount')
    amount.toggleClass 'hidden'
    ranges = $('input.bet-amount:visible')
    ranges.prop 'max', data.balance
    if data.balance < 10
      ranges.prop 'step', data.balance
    else
      ranges.prop 'step', 10
    amount.prop 'value', data.amount
    ranges.trigger 'change'
    $('#balance').text data.balance
    if data.action == 'disable'
      form.prop 'method', 'delete'
      method.prop 'value', 'delete'
      choices.prop 'disabled', true
      button.removeClass 'btn-success'
      button.addClass 'btn-danger'
      button.text '<%= I18n.t :CANCEL %>'
    else
      form.prop 'method', 'post'
      method.prop 'value', 'post'
      choices.prop 'disabled', false
      button.removeClass 'btn-danger'
      button.addClass 'btn-success'
      button.text '<%= I18n.t :BET %>'
